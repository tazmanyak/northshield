//****************************************************************************
//SPHERE by : Menasoft ©1997-2007
//www.sphereserver.net
// All SPHERE script files and formats are copyright Menasoft & Partners.
// This file may be freely edited for personal use, but may not be distributed
// in whole or in part, in any format without express written permission from
// Menasoft & Partners.  All donations and contributions
// become the property of Menasoft & Partners.
//****************************************************************************
// FILE LAST UPDATED: Tuesday, January 19, 2008

VERSION=0.56b

[DEFNAME backward_compability_defs]
loot_ORGE_LORD			loot_OGRE_LORD
i_axe_war_VANQUISIHNG		i_axe_war_VANQ
i_scroll_liche_transformation	i_scroll_lich_form
s_liche_form			s_lich_form
i_liche_fx			i_lich_fx
i_liche_form			i_lich_form
c_liche				c_lich
c_m_liche_lord			c_m_lich_lord
c_spider_lord		
i_candleabra	i_candelabra
i_lantern_hanging_red i_lantern_paper4
i_lantern_hanging_white i_lantern_paper5

[DEFNAME backward_compatibility_defs_scp]
// weapons
i_staff_bladed_double		i_double_bladed_staff
i_staff_bladed			i_bladed_staff
i_crossbow_repeating		i_repeating_crossbow
i_bow_composite			i_composite_bow
i_sword_paladin			i_paladin_sword
i_mace_wildstaff		i_wild_staff
i_sword_warcleaver		i_war_cleaver
i_sword_leafblade		i_leafblade
i_mace_diamond			i_diamond_mace
i_sword_assassin_spike		i_assassin_spike
i_bow_ruin_o			i_bow_ruin
i_kryss_ruin_o			i_kryss_ruin
// craftables
i_armoir_lt			i_armoire_lt
i_armoir_dk			i_armoire_dk
i_tambourine_tas		i_tambourine_tassel
i_lyre				i_harp_lap
i_harp_large			i_harp_standing


[FUNCTION addnpc]
IF (<ISNUM <ARGS>>)
	ADD <ARGS>|08c000000
ELSE
	ADD <ARGS>
ENDIF

[FUNCTION tweak]
INFO

[function brain] //by Darkandus
if !(<ARGN>)
	return <NPC>
else
	NPC=<ARGN>
endif

[FUNCTION m]
MOVE <ARGS>

[FUNCTION invulnerable]
INVUL <ARGS>

[FUNCTION invisible]
INVIS <ARGS>

[FUNCTION static] //by Thomas20198701
IF !(<ARGN>)
	IF (<ISEMPTY <DEF.<ARGS>>>)
		SYSMESSAGE Not a valid command or format.
		RETURN 0
	endif
endif
IF !(<ISEMPTY <ARGS>>)
	IF !(<SERV.ITEMDEF.<ARGS>>)
		SYSMESSAGE Not a valid command or format.
		RETURN 0
	endif
	TARGETFG static_ <ARGS>
	SYSMESSAGE Where would you like to place the <SERV.ITEMDEF.<ARGS>.NAME>?
	RETURN 1
endif
SYSMESSAGE Not a valid command or format.
RETURN 0



[function static_]
serv.newitem <args>
new.p=<targp>
IF (<NEW.TYPE>==t_multi)
NEW.MULTICREATE
ENDIF
new.attr |= attr_move_never
new.timer -1
act=<new>

[FUNCTION sfx]
SOUND <ARGS>

[FUNCTION music]
IF (<ISCHAR>)
	IF (<ISPLAYER>)
		MIDILIST <ARGS>
	endif
endif

[FUNCTION cliver]
RETURN <CLIENTVERSION>

[FUNCTION clientver]
RETURN <CLIENTVERSION>

[FUNCTION FOLLOW]
SRC.SYSMESSAGE  Follow: on - Use war mode to stop following
SERV.NEWITEM i_eq_followSrc
IF ( !<ARGN> )
	NEW.LINK=<UID>
ELSE
	NEW.LINK=<ARGN>
endif
NEW.TIMER=2
SRC.EQUIP <NEW.UID>

[ITEMDEF i_eq_followSrc]
ID=i_handr_1
NAME="Follower"
TYPE=T_eq_script
LAYER=layer_special
WEIGHT=0

ON=@targon_Char
	link=<argo>

ON=@targon_Item
	link=<argo>

ON=@timer
	if (!(<link.uid>) || !(<topobj.ischar>))
		remove
		return 0
	endif
	if (<topobj.flags> & statf_war)
		topobj.sysmessage Follow:off
		remove
		return 0
	endif
	topobj.face <link.uid>
	topobj.p <link.topobj.p>
	topobj.update
	timer 2
	return 1

[ITEMDEF i_gate_wood_2]
DUPEITEM=i_fence_wood_3

//
// This function can be used to force a character to cast a specific
// spell. Works on both players and NPCs. Two arguments can be given:
//
// "NpcCast <spell>, <target>"
// spell  -->  spell id to cast
// target -->  target (optional, default: previous action's target)
//             - can be a uid or map location
//
[FUNCTION npccast]
// Determine spell and skill used to cast
LOCAL.SPELL = <EVAL <ARGV[0]> &~ 0ff000000>
LOCAL.SKILL = <EVAL (<STREAT <SERV.SPELL.<LOCAL.SPELL>.SKILLREQ>>) &~ 0ff000000>
IF !(<LOCAL.SKILL>)
	LOCAL.SKILL = 25
ENDIF

// Set spell target
IF (<ARGV> > 2)
	ACT = 0
	ACTP = <ARGV[1]>, <ARGV[2]>, <ARGV[3]>, <ARGV[4]>
ELSE
	REF1 = <ARGV[1]>
	IF !(<REF1.UID>)
		REF1 = <ACT.UID>
		IF !(<REF1.UID>)
			REF1 = <UID>
		ENDIF
	ENDIF
	ACT = <REF1.UID>
	ACTP = <REF1.P>
ENDIF

// Set character action
ACTPRV = <UID>
ACTARG1 = <LOCAL.SPELL>
ACTION = <LOCAL.SKILL>
RETURN 1


[EOF]
