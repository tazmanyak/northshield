//****************************************************************************
// SphereServer ©1997-2009
// This script is part from the Sphere Community Script Pack.
// You can download the full pack from: www.sphereserver.net
//****************************************************************************
// FILE LAST UPDATED:
VERSION=0.56b
//
// SCRIPT CREDITS
// --------------
// pyromapes2k - Base crafting system used in here
// coruja - Updated to be more OSI compatible
// ShiryuX - Updated to the scriptpack and tweaked many parts of it
//
// SCRIPT DOCUMENTATION
// --------------------
// OSI Crafting System
// ---------------------------------------
// ctag0.craft.skill = the skill used
// ctag0.craft.last = last item crafted
// ctag0.craft.page.sidebar = sidebar page
// ---------------------------------------
// d_craft_menu buttons
// --------------------
// 1 - 99 = sidebar
// 100 - 300 = item list
// 600 - 800 = item list (make button)
//
// 400 = prev page sidebar
// 401 = next page sidebar
//
// 403 = prev page main list
// 404 = next page main list
//
// 406 = last ten button
//
// 500 = smelt item
// 501 = first resource
// 502 = second resource
// 503 = exit
//
// 505 = repair
// 506 = smelt
// 507 = enhance
// 508 = make last
// --------------------

///////////////////////////////////////////////////////////////
//////// MAIN CRAFTING MENU
///////////////////////////////////////////////////////////////
// This is the main menu, contains the list of items.
[dialog d_craft_menu]
40,40
src.closealldialogs
page 0
//resizepic 0 0 9200 530 437	// background/border
//gumppictiled 10 10 510 417 2624	// darker background
//gumppictiled 10 32 510 5 5058	// 1 grey separators - start here
//gumppictiled 10 287 510 5 5058	// 2
//gumppictiled 10 337 510 5 5058	// 3
//gumppictiled 210 37 5 250 5058	// 4
//gumppictiled 160 292 5 45 5058	// 5 grey separators - end here

resizepic 10 10 2620 510 417
resizepic 10 32 2620 510 5
resizepic 10 287 2620 510 5
resizepic 10 337 2620 510 5
resizepic 210 37 2620 5 250
resizepic 160 292 2620 5 45

//checkertrans 10 10 510 417	// transparency layer
xmfhtmlgumpcolor 10 13 530 20 <ddef.scp.craft_title_<ctag0.craft.skill>> 0 0 <ddef.dcolor_white> // menu header

page 1
dhtmlgump 10 38 200 20 0 0 <DEF.BFONT_white>        KATEGORILER
//xmfhtmlgumpcolor 50 61 115 20 <ddef.scp.craft_button_lastten> 0 0 <ddef.dcolor_white>
//button 15 60 4005 4007 1 0 5010
local.wid = 80
local.sidebar = <eval 1 + (10 * <ctag0.craft.page.sidebar>)>
for z <local.sidebar> <eval <local.sidebar>+9>
	if !<isempty <def.<ctag0.craft.skill>_category_<dlocal.z>_name>>
	button 15 <dlocal.wid> 4005 4007 1 0 <local.sidebar>
		if (<isnum <def.<ctag0.craft.skill>_category_<dlocal.z>_name>>)
		xmfhtmlgumpcolor 50 <eval <local.wid>+2> 115 20 <ddef.<ctag0.craft.skill>_category_<dlocal.z>_name> 0 0 <ddef.dcolor_white>
		else
		dcroppedtext 50 <eval <local.wid>+2> 115 20 0480 <def.<ctag0.craft.skill>_category_<dlocal.z>_name>
		endif
	endif
local.wid += 20
local.sidebar ++
endfor
//if !<isempty <def.<ctag0.craft.skill>_category_<dlocal.sidebar>_name>>
//button 135 267 4005 4007 1 0 401
//endif
//if <ctag0.craft.page.sidebar> > 0
//button 15 267 4014 4015 1 0 400
//endif

// notices section
dhtmlgump 10 305 155 20 0 0 <DEF.BFONT_white>  Uyarilar :
if <isnum <ctag.craft.message>>
xmfhtmlgumpcolor 175 295 350 45 <dctag0.craft.message> 0 0 <ddef.dcolor_white>
else
dhtmlgump 175 295 350 45 0 0 <def.bfont_white><ctag.craft.message>
endif

// item list section
dhtmlgump 215 38 305 20 0 0 <DEF.BFONT_white>               SECENEKLER

local.y = 60
local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
local.mainbar = <eval 1 + (10 * <ctag0.craft.page.mainbar>)>
for 1 10
local.item = <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_<dlocal.mainbar>>
	if <local.item> != 0
	button 220 <local.y> 4005 4007 1 0 <eval 100 + <local.mainbar>>
	//button 220 <local.y> 4005 4007 1 0 <eval 600 + <local.mainbar>>
	dcroppedtext 255 <local.y> 220 20 <qval (<canmake <local.item>>) ? 0480 : 020> <serv.itemdef.<local.item>.name>
	endif
local.mainbar ++
local.y += 20
endfor
if !<isempty <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_<dlocal.mainbar>>>
button 380 267 4005 4007 1 0 404
DHTMLGUMP 415 268 150 20 0 0 <DEF.BFONT_white>SONRAKI SAYFA
endif
if (<ctag0.craft.page.mainbar> > 0)
button 220 267 4014 4015 1 0 403
DHTMLGUMP 255 268 150 20 0 0 <DEF.BFONT_white>ONCEKI SAYFA
endif

// buttons - left section
if !<isempty <def.<ctag0.craft.skill>_smelt>>
button 15 342 4005 4007 1 0 500
xmfhtmlgumpcolor 50 343 150 20 <ddef.scp.craft_button_smelt> 0 0 <ddef.dcolor_white>
endif
if <def0.<ctag0.craft.skill>_firstmaterial>
button 15 362 4005 4007 1 0 501
dtext 50 363 0480 <def.<ctag0.craft.skill>_material_<dctag.craft.material.<ctag.craft.skill>>_name>
endif
//if <def0.<ctag0.craft.skill>_secondmaterial>
//button 15 382 4005 4007 1 0 502
//dtext 50 383 0480 <def.<ctag0.craft.skill>_material_2_<dctag.craft.material.<ctag.craft.skill>>_name>
//endif
button 15 402 4017 4019 1 0 503
dhtmlgump 50 403 150 20 0 0 <DEF.BFONT_white>KAPAT

// buttons - right section
button 270 342 4005 4007 1 0 505
dhtmlgump 305 343 150 20 0 0 <DEF.BFONT_white> Esya Tamiri
button 270 362 4005 4007 1 0 506
dhtmlgump 305 363 150 20 0 0 <DEF.BFONT_white> Hafizayi Sil
button 270 382 4005 4007 1 0 507
dhtmlgump 305 383 150 20 0 0 <DEF.BFONT_white> Esyayi Gelistir(Kapali)
button 270 402 4005 4007 1 0 508
dhtmlgump 305 403 150 20 0 0 <DEF.BFONT_white> Tekrar Yap


[dialog d_craft_menu button]
on=0 // exit
ctag.craft.message

on=1 99 // select category button
ctag.craft.item
ctag.craft.page.mainbar
ctag.craft.sidebar = <argn>
f_resenddialog d_craft_menu

on=100 300 // select item button

local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
if <ctag0.craft.material.<ctag.craft.skill>>
	if <isempty <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_<eval <argn>-100>>>
	local.resource = "_"
	endif
endif
ctag.craft.item = <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_<eval <argn>-100>>
ctag.craft.item.options = <def.<ctag0.craft.skill>_options_default>
if !<isempty <def.<ctag0.craft.skill>_category_<dctag0.craft.sidebar>_<eval <argn>-100>_opt>>
ctag.craft.item.options = <def.<ctag0.craft.skill>_category_<dctag0.craft.sidebar>_<eval <argn>-100>_opt>
endif
dialogclose d_craft_menu
f_resenddialog d_craft_itemprop

on=600 800 // craft item button
local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
if <ctag0.craft.material.<ctag.craft.skill>>
	if <isempty <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_name>>
	local.resource = "_"
	endif
endif
ctag.craft.item = <def.<ctag0.craft.skill><local.resource>category_<dctag0.craft.sidebar>_<eval <argn>-600>>
ctag.craft.item.options = <def.<ctag0.craft.skill>_options_default>
if !<isempty <def.<ctag0.craft.skill>_category_<dctag0.craft.sidebar>_<eval <argn>-600>_opt>>
ctag.craft.item.options = <def.<ctag0.craft.skill>_category_<dctag0.craft.sidebar>_<eval <argn>-600>_opt>
endif
craft <ctag0.craft.item>


on=400 401 // sidebar buttons
if <argn> == 401
ctag0.craft.page.sidebar ++
else
ctag0.craft.page.sidebar --
endif
f_resenddialog d_craft_menu

on=403 404 // main list buttons
if <argn> == 404
ctag0.craft.page.mainbar ++
else
ctag0.craft.page.mainbar --
endif
f_resenddialog d_craft_menu

on=406 // last ten button
// if someone knows what it does script it

on=500 // smelt button
f_resenddialog d_craft_menu
try <def.<ctag0.craft.skill>_smelt>

on=501 502 // material buttons
dialogclose d_craft_menu
f_resenddialog d_craft_material <eval <argn> - 500>

on=503 // exit button
ctag.craft.message
dialogclose d_craft_menu

on=505 // repair button
//f_resenddialog d_craft_menu
//return 1
if !<isempty <def.<ctag0.craft.skill>_repair>>
try <def.<ctag0.craft.skill>_repair>
return 1
endif
f_resenddialog d_craft_menu

on=506 // mark item option button
if <def0.craft_<ctag0.craft.skill>_marking>
	doswitch <ctag0.craft.mark>
	ctag.craft.mark = 1
	ctag.craft.mark = 2
	ctag.craft.mark
	enddo
endif
f_resenddialog d_craft_menu

on=507 // enhance button
f_resenddialog d_craft_menu

on=508 // make last button
if <isempty <ctag.craft.last.<ctag0.craft.skill>>>
ctag.craft.message = Oncesinde bir item yapmaniz gerekir.
return 1
endif
craft <ctag0.craft.last.<ctag0.craft.skill>>


///////////////////////////////////////////////////////////////
//////// MATERIAL CRAFTING MENU
///////////////////////////////////////////////////////////////
// This is the main menu, contains the list of items.
[dialog d_craft_material]
40,40

page 0

resizepic 10 10 2620 510 417
resizepic 10 32 2620 510 5
resizepic 10 287 2620 510 5
resizepic 10 337 2620 510 5
resizepic 210 37 2620 5 250
resizepic 160 292 2620 5 45

//checkertrans 10 10 510 417	// transparency layer
xmfhtmlgumpcolor 10 13 530 20 <ddef.scp.craft_title_<ctag0.craft.skill>> 0 0 <ddef.dcolor_white> // menu header

page 1
// sidebar section
xmfhtmlgumpcolor 10 38 200 20 <ddef.scp.craft_head_categories> 0 0 <ddef.dcolor_white>
//button 15 60 4005 4007 1 0 5010
//xmfhtmlgumpcolor 50 61 115 20 <ddef.scp.craft_button_lastten> 0 0 <ddef.dcolor_white>
local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
local.sidebar = <eval 1 + (10 * <ctag0.craft.page.sidebar>)>
local.wid = 80
if <ctag0.craft.material.<ctag.craft.skill>>
	if <isempty <def.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name>>
	local.resource = "_"
	endif
endif
for z <local.sidebar> <eval <local.sidebar>+9>
	if !<isempty <def.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name>>
	button 15 <dlocal.wid> 4005 4007 1 0 <local.sidebar>
		if (<isnum <def.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name>>)
		xmfhtmlgumpcolor 50 <eval <local.wid>+2> 115 20 <ddef.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name> 0 0 <ddef.dcolor_white>
		else
		dcroppedtext 50 <eval <local.wid>+2> 115 20 0480 <def.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name>
		endif
	endif
local.wid += 20
local.sidebar ++
endfor
if !<isempty <def.<ctag0.craft.skill><local.resource>category_<dlocal.sidebar>_name>>
button 135 267 4005 4007 1 0 401
endif
if <ctag0.craft.page.sidebar> > 0
button 15 267 4014 4015 1 0 400
endif

// notices section
xmfhtmlgumpcolor 10 305 155 20 <ddef.scp.craft_head_notices> 0 0 <ddef.dcolor_white>
if <isnum <ctag.craft.message>>
xmfhtmlgumpcolor 175 295 350 45 <dctag0.craft.message> 0 0 <ddef.dcolor_white>
else
dhtmlgump 175 295 350 45 0 0 <def.bfont_white><ctag.craft.message>
endif

// material list section
xmfhtmlgumpcolor 215 38 305 20 <ddef.scp.craft_head_selections> 0 0 <ddef.dcolor_white>
local.mainbar = <eval 10 * <ctag0.craft.page.mainbar>>
local.y = 60
for 0 10
	if !<isempty <def.<ctag0.craft.skill>_material_<dlocal.mainbar>_name>>
	button 220 <local.y> 4005 4007 1 0 <eval 600 + <local.mainbar>>
	dcroppedtext 255 <local.y> 220 20 0480 <def.<ctag0.craft.skill>_material_<dlocal.mainbar>_name>
	endif
local.mainbar ++
local.y += 20
endfor
if !<isempty <def.<ctag0.craft.skill>_material_<dlocal.mainbar>_name>>
button 380 267 4005 4007 1 0 404
xmfhtmlgumpcolor 415 268 150 20 <ddef.scp.craft_button_nextpage> 0 0 <ddef.dcolor_white>
endif
if (<ctag0.craft.page.mainbar> > 0)
button 220 267 4014 4015 1 0 403
xmfhtmlgumpcolor 255 268 150 20 <ddef.scp.craft_button_prevpage> 0 0 <ddef.dcolor_white>
endif

// buttons - left section
if !<isempty <def.<ctag0.craft.skill>_smelt>>
button 15 342 4005 4007 1 0 500
xmfhtmlgumpcolor 50 343 150 20 <ddef.scp.craft_button_smelt> 0 0 <ddef.dcolor_white>
endif
if <def0.<ctag0.craft.skill>_firstmaterial>
button 15 362 4005 4007 1 0 501
dtext 50 363 0480 <def.<ctag0.craft.skill>_material_<dctag.craft.material.<ctag.craft.skill>>_name>
endif
//if <def0.<ctag0.craft.skill>_secondmaterial>
//button 15 382 4005 4007 1 0 502
//dtext 50 383 0480 <def.<ctag0.craft.skill>_material_2_<dctag.craft.material.<ctag.craft.skill>>_name>
//endif
button 15 402 4017 4019 1 0 503
xmfhtmlgumpcolor 50 403 150 20 <ddef.scp.craft_button_exit> 0 0 <ddef.dcolor_white>

// buttons - right section
button 270 342 4005 4007 1 0 505
xmfhtmlgumpcolor 305 343 150 20 <ddef.scp.craft_button_repair> 0 0 <ddef.dcolor_white>
button 270 362 4005 4007 1 0 506
xmfhtmlgumpcolor 305 363 150 20 <qval (<ctag0.craft.mark>) ? <qval (<ctag0.craft.mark>==1) ? 1044017 : 1044019> : 1044018> 0 0 <ddef.dcolor_white>
button 270 382 4005 4007 1 0 507
xmfhtmlgumpcolor 305 383 150 20 <ddef.scp.craft_button_enhance> 0 0 <ddef.dcolor_white>
button 270 402 4005 4007 1 0 508
xmfhtmlgumpcolor 305 403 150 20 <ddef.scp.craft_button_makelast> 0 0 <ddef.dcolor_white>

[dialog d_craft_material button]
on=0

on=400 401 // sidebar buttons
if <argn> == 401
ctag0.craft.page.sidebar ++
else
ctag0.craft.page.sidebar --
endif
f_resenddialog d_craft_material

on=403 404 // mainbar buttons
if <argn> == 404
ctag0.craft.page.mainbar ++
else
ctag0.craft.page.mainbar --
endif
f_resenddialog d_craft_material

on=600 700
if <argn> == 600
ctag.craft.material.<ctag0.craft.skill>
f_resenddialog d_craft_material
return 0
endif
local.skill = <strarg <def.<ctag0.craft.skill>_material_<eval <argn>-600>_skill>>
local.skillreq = <streat <def.<ctag0.craft.skill>_material_<eval <argn>-600>_skill>>
if <<local.skill>> < <local.skillreq>
ctag.craft.message "Bu malzemeyi yapmak icin en az <fval <local.skillreq>> <local.skill> gerekir."
f_resenddialog d_craft_material
return 1
endif
ctag.craft.material.<ctag0.craft.skill> = <eval <argn> - 600>
f_resenddialog d_craft_menu

///////////////////////////////////////////////////////////////
//////// ITEM PROPERTIES CRAFTING MENU
///////////////////////////////////////////////////////////////
// This menu displays the requirements of the item to craft
[dialog d_craft_itemprop]
40,40

page 0 // Basic Layout
//resizepic 0 0 9200 530 418 // background

resizepic 10 10 2620 510 399
resizepic 10 32 2620 510 5
resizepic 10 185 2620 155 5
resizepic 10 212 2620 155 25
resizepic 165 212 2620 355 5
resizepic 10 297 2620 155 5
resizepic 165 297 2620 355 5
resizepic 160 37 2620 5 148
resizepic 165 127 2620 355 5
resizepic 10 382 2620 510 5






//checkertrans 10 10 510 399

page 1 // Content
xmfhtmlgumpcolor 10 13 530 20 <ddef.scp.craft_title_<ctag0.craft.skill>> 0 0 <ddef.dcolor_white>
args = <ctag0.craft.item>
serv.newitem <argv[0]>
ref1=<new>
if <def.scp.NewCrafting_RecipeNeeded> == 1
local.recipe = <getargv 3 <ctag0.craft.item.options>>
	if <tag0.recipe.<new.baseid>>
	local.recipe = 0
	endif
endif
tilepichue 30 80 <ref1.dispiddec> <dref1.color>
dcroppedtext 170 37 265 20 0480 ITEM
dcroppedtext 330 37 265 20 88 <ref1.name>
dcroppedtext 170 74 330 20 0480 Success Chance
if <local.recipe> == 1
dcroppedtext 430 74 50 20 0480 ??.?%
else
dcroppedtext 430 74 50 20 0480 <fval <ref1.craftsuccesschance <ref1.baseid>>>%
endif
button 15 387 4014 4016 1 0 0
dtext 50 388 0480 Back
button 270 387 4011 4012 1 0 1
dtext 305 388 0480 ESYAYI YAP
args = <ref1.skillmake>
dorigin 170 112
for 1 <argv>
	if (<serv.skill.<ref1.skillmake.<local._for>.key>>)
		if <local.recipe> == 1
		dcroppedtext 170 *20 170 20 0480 ??????????
		dcroppedtext 430 *0 50 20 0480 ??.?
		else
		dcroppedtext 170 *20 170 20 0480 <serv.skill.<ref1.skillmake.<local._for>.key>.name>
		dcroppedtext 430 *0 50 20 0480 <fval <ref1.skillmake.<local._for>.val>>
		endif
	endif
endfor
xmfhtmlgumpcolor 10 215 155 20 1044055 0 0 <ddef.dcolor_white>
args = <ref1.resmake>
dorigin 170 197
for 1 <argv>
	if <local.recipe> == 1
	dcroppedtext 170 *20 170 20 0480 ??????????
	dcroppedtext 430 *0 50 20 0480 ??
	else
	dcroppedtext 170 *20 170 20 0480 <serv.itemdef.<ref1.resources.<local._for>.key>.name>
	dcroppedtext 430 *0 50 20 0480 <ref1.resources.<local._for>.val>
	endif
endfor
ref1.remove
local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
args = <ctag0.craft.item.options> // mark,exceptional,colored,recipe,expansion
local.height = 302
if !<isempty <argv[4]>>
	if <argv[4]> > 3
	xmfhtmlgumpcolor 170 <dlocal.height> 350 20 <ddef.scp.craft_resdisp_<argv[4]>> 0 0 <ddef.dcolor_white>
	local.height += 20
	endif
endif
if <argv[2]>
xmfhtmlgumpcolor 170 <dlocal.height> 350 20 <ddef.scp.craft_colored> 0 0 <ddef.dcolor_white>
local.height += 20
endif
if <argv[0]>
xmfhtmlgumpcolor 170 <dlocal.height> 350 20 <ddef.scp.craft_markable> 0 0 <ddef.dcolor_white>
local.height += 20
endif

[dialog d_craft_itemprop button]
on=0
ctag.craft.item
dialogclose d_craft_itemprop
f_resenddialog d_craft_menu

on=1
local.resource = <qval <ctag0.craft.material.<ctag0.craft.skill>> ? "_<dctag0.craft.material.<ctag0.craft.skill>>_" : "_">
if <ctag0.craft.material.<ctag.craft.skill>>
	if <isempty <def.<ctag0.craft.skill><local.resource>category_<dlocal.z>_name>>
	local.resource = "_"
	endif
endif
src.closealldialogs
craft <ctag0.craft.item>
return 1

/////////////////////////////////////////
/////////////////////////////////////////
/////////////////////////////////////////

[dialog d_craft_prompt]
0,25

resizepic 0 0 5054 220 170
resizepic 10 10 3000 200 150
dhtmlgump 20 20 180 80 0 0 "Bu itemi uretmek icin isaret koymak istiyormusunuz ?"
dhtmlgump 55 100 140 25 0 0 CONTINUE
button 20 100 4005 4007 1 0 1
dhtmlgump 55 125 140 25 0 0 CANCEL
button 20 125 4005 4007 1 0 0

[dialog d_craft_prompt button]
on=0
src.sysmessage @,,1 Iptal edildi.

on=1
src.sysmessage @,,1 Item isaretlendi.
tag0.exceptional = <src.name>

[eof]
