// Spells done:
// 201 - Cleanse by Fire
// 202 - Close Wounds
// 203 - Consecrate Weapon
// 204 - Dispel Evil
// 205 - Divine Fury
// 206 - Enemy of One
// 207 - Holy Light
// 208 - Noble Sacrifice
// 209 - Remove Curse
// 210 - Sacred Journey

[Spell 201]
DEFNAME=s_Cleanse_By_Fire
NAME=Cleanse By Fire
SOUND=0
RUNES=.@,,2 1060718
CAST_TIME=1.0
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_targ_char|spellflag_fx_targ|spellflag_good|spellflag_no_castanim|spellflag_playeronly
EFFECT_ID=0373A
EFFECT=10
DURATION=0.0
MANAUSE=10
SKILLREQ=SKILL_CHIVALRY 5.0
INTERRUPT=0.0,0.0

ON=@Effect
SRC.SOUND=0208
SOUND=01E0
SRC.EFFECT 3,03709,3,20
LOCAL.DAMAGE=<EVAL <FLOATVAL 15+(50-<SRC.F_COMPUTE_POWERLEVEL 4>)>/1000000>
IF (<LOCAL.DAMAGE> > 50)
LOCAL.DAMAGE=50
ELIF (<LOCAL.DAMAGE> < 10)
LOCAL.DAMAGE=10
ENDIF
SRC.DAMAGE <LOCAL.DAMAGE> <DEF.DAM_MAGIC> <SRC>
LOCAL.CHANCETOCURE=<EVAL <FLOATVAL (10000+(<SRC.CHIVALRY>*75)-((<FINDID.i_rune_poison.F_POISONLEVEL>+1)*2000))/100>/1000000>
IF (<LOCAL.CHANCETOCURE> >= <R1,100>)
	IF (<SRC.UID> != <UID>)
	SRC.SYSMESSAGE @,,2 1010058
	ENDIF
SYSMESSAGE @,,2 1010059
SPELLEFFECT S_CURE,1000.0,<SRC>
ELSE
SRC.SYSMESSAGE @,,2 1010060
ENDIF

[Spell 202]
DEFNAME=s_Close_Wounds
NAME=Close Wounds
SOUND=0202
RUNES=.@,,2 1060719
CAST_TIME=1.5
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_targ_char|spellflag_fx_targ|spellflag_good|spellflag_no_castanim|spellflag_playeronly
EFFECT_ID=0376A
EFFECT=10
DURATION=0.0
MANAUSE=10
SKILLREQ=SKILL_CHIVALRY 0.1
INTERRUPT=0.0,0.0

ON=@Success
IF (<ACT.DISTANCE> > 2)
SYSMESSAGE @,,2 1060178
RETURN 1
ELIF (<ACT.NPC> == brain_undead)
SYSMESSAGE @,,2 1061654
RETURN 1
ELIF (((<ACT.FLAGS>&STATF_Poisoned) || (<ACT.TAG0.MORTALWOUNDED>)) && (<ACT.UID> == <UID>))
MESSAGE @,,2 1005000
RETURN 1
ELIF ((<ACT.FLAGS>&STATF_Poisoned) || (<ACT.TAG0.MORTALWOUNDED>))
ACT.MESSAGE @,,2 1010398
RETURN 1
ELIF (<ACT.HITS> >= <ACT.MAXHITS>)
SYSMESSAGE @,,2 500955
RETURN 1
ENDIF

ON=@Effect
LOCAL.TOHEAL=<EVAL <SRC.F_COMPUTE_POWERLEVEL 6>+<R0,2>>
IF (<LOCAL.TOHEAL> < 7)
LOCAL.TOHEAL=7
ELIF (<LOCAL.TOHEAL> > 40)
LOCAL.TOHEAL=40
ENDIF
IF (<EVAL <LOCAL.TOHEAL>+<HITS>> > <MAXHITS>)
LOCAL.TOHEAL=<EVAL <MAXHITS>-<HITS>>
ENDIF
HITS += <LOCAL.TOHEAL>
SYSMESSAGE @,,2 1060203,<dLOCAL.TOHEAL>
EFFECT 2,03779,3,30

[Spell 203]
DEFNAME=s_Consecrate_Weapon
NAME=Consecrate Weapon
SOUND=
RUNES=.@,,2 1060720
CAST_TIME=0.1
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=10
DURATION=0.0
MANAUSE=10
SKILLREQ=SKILL_CHIVALRY 15.0
INTERRUPT=0.0,0.0

ON=@Effect
IF !(<WEAPON>)
SYSMESSAGE @,,2 501078
RETURN 1
ELSE
    IF (<WEAPON.SKILL> == 31) //Archery
    LOCAL.ITEMID=013B1
    LOCAL.SOUND=0145
    ELIF (<WEAPON.SKILL> == 41) //Mace
    LOCAL.ITEMID=0FB4
    LOCAL.SOUND=0232
    ELSE
    LOCAL.ITEMID=0F5F
    LOCAL.SOUND=056
    ENDIF
SOUND 020C
SOUND <LOCAL.SOUND>
EFFECT 3,03779,3,30,,9964
SENDPACKET 0C7 00 D00 D00 W<LOCAL.ITEMID> W<HVAL <P.X>> W<HVAL <P.Y>> <HVAL <P.Z>> W<HVAL <P.X>> W<HVAL <P.Y>> <HVAL <P.Z>+50> 01 00 W00 00 00 D021 D03 W0251D W01 W00 D077 00 W0100
LOCAL.DELAY=<F_COMPUTE_POWERLEVEL 20>
    IF (<LOCAL.DELAY> < 3)
    LOCAL.DELAY=3
    ELIF (<LOCAL.DELAY> > 12)
    LOCAL.DELAY=12
    ENDIF
TAG0.DAMINCREASE += 10
TAG0.LIFELEECH += 20
TIMERF <LOCAL.DELAY>,F_CONSECRATE_WEAPON_EXPIRE
ENDIF

[Spell 204]
DEFNAME=s_Dispel_Evil
NAME=Dispel Evil
SOUND=
RUNES=.@,,2 1060721
CAST_TIME=0.3
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=10
DURATION=0.0
MANAUSE=10
SKILLREQ=SKILL_CHIVALRY 35.0
INTERRUPT=0.0,0.0

ON=@Effect
LOCAL.POWER=<F_COMPUTE_POWERLEVEL 2>
SOUND=0299
EFFECT 3,037C4,3,30
FORCHARS 7
    IF (<ISFIGHTINGME <SRC.UID>>)
        IF (<CANSEELOS>)
            IF (!(<ISPLAYER>))
                IF (<FLAGS>&STATF_CONJURED)
                    IF (<LOCAL.POWER> >= <R1,100>)
                    SPELLEFFECT S_DISPEL,<EVAL <LOCAL.POWER>*10>,<SRC>
                    ENDIF
                ELSE
                LOCAL.FLEECHANCE=<FLOATVAL (SQRT(((<FAME>/2)*<SRC.CHIVALRY>*<dLOCAL.POWER>)))>
                LOCAL.FLEECHANCE=<FLOATVAL <LOCAL.FLEECHANCE>/10000>
                LOCAL.FLEECHANCE /= 1000000
                    IF (<LOCAL.FLEECHANCE> >= <R1,100>)
                    FLAGS &= ~statf_war
                    FLEE
                    ENDIF
                ENDIF
            ENDIF
        ENDIF
    ENDIF
ENDFOR

[Spell 205]
DEFNAME=s_Divine_Fury
NAME=Divine Fury
SOUND=020F
RUNES=.@,,2 1060722
CAST_TIME=1.0
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=0376A
EFFECT=10
DURATION=0.0
MANAUSE=15
SKILLREQ=SKILL_CHIVALRY 25.0
INTERRUPT=0.0,0.0

ON=@Effect
IF (!(<TAG0.DIVINEFURYEFFECT>))
EFFECT 3,0376A,3,30,0,1160
EFFECT 3,037C4,3,30,0,43
    IF (<BODY> == c_man) || (<BODY> == c_elf_male) || (<BODY> == c_man_gm)
    SOUND=044A
    ELSE
    SOUND=0338
    ENDIF
LOCAL.DELAY=<F_COMPUTE_POWERLEVEL 10>
    IF (<LOCAL.DELAY> < 7)
    LOCAL.DELAY=7
    ENDIF
    IF (<LOCAL.DELAY> > 27)
    LOCAL.DELAY=27
    ENDIF
STAM=<MAXSTAM>
TAG0.DAMINCREASE += 10
TAG0.SWINGINCREASE += 10
TAG0.HITCHANCEINCREASE += 10
TAG0.HITCHANCEDECREASE -= 20
TAG.DIVINEFURYEFFECT=1
ADDBUFF 1010 1060589 1075634 <dLOCAL.DELAY> oi
TIMERF <dLOCAL.DELAY>,F_DIVINE_FURY_CALLBACK
ENDIF

[Spell 206]
DEFNAME=s_Enemy_of_One
NAME=Enemy of One
SOUND=00F5
RUNES=.@,,2 1060723
CAST_TIME=0.5
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=0376A
EFFECT=10
DURATION=0.0
MANAUSE=20
SKILLREQ=CHIVALRY 45.0
INTERRUPT=0.0,0.0

ON=@Effect
SOUND=01ED
EFFECT 3,0375A,3,30,0,33
EFFECT 3,037B9,3,30,0,43
LOCAL.DELAY=<F_COMPUTE_POWERLEVEL 1>
IF (<LOCAL.DELAY> > <EVAL (60*35)/10>)
LOCAL.DELAY=210
ENDIF
REMOVEBUFF 1011
TAG.ENEMYOFONE.TARGET=
EVENTS -E_ENEMY_OF_ONE
IF (<FINDID.i_enemyofone_timer>)
FINDID.i_enemyofone_timer.TIMER=<LOCAL.DELAY>
ELSE
SERV.NEWITEM i_enemyofone_timer,1,<UID>
NEW.TIMER=<LOCAL.DELAY>
ENDIF
EVENTS +E_ENEMY_OF_ONE
f_AddBuff EnemyOfOne,<dlocal.delay>,50,#1075975,#1018111,100

[Spell 207]
DEFNAME=s_Holy_Light
NAME=Holy Light
SOUND=
RUNES=.@,,2 1060724
CAST_TIME=1.8
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=10
DURATION=0.0
MANAUSE=10
SKILLREQ=CHIVALRY 55.0
INTERRUPT=0.0,0.0

ON=@Effect
LOCAL.CASTER=<UID>
LOCAL.DAMAGE=<EVAL <F_COMPUTE_POWERLEVEL 10>+<R0,4>>
SOUND=0212
SOUND=0206
EFFECT 2,0375A,3,30,0,07f4
EFFECT 2,037C4,3,30,0,07f4
FORCHARS 3
	IF (<ISFIGHTINGME <LOCAL.CASTER>>)
		IF (<CANSEELOS <LOCAL.CASTER>>)
		DAMAGE <LOCAL.DAMAGE> <DEF.dam_energy> <LOCAL.CASTER>
		ENDIF
	ENDIF
ENDFOR

[Spell 208]
DEFNAME=s_Noble_Sacrifice
NAME=Noble Sacrifice
SOUND=
RUNES=.@,,2 1060725
CAST_TIME=1.5
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=30
DURATION=0.0
MANAUSE=20
SKILLREQ=SKILL_CHIVALRY 65.0
INTERRUPT=0.0,0.0

ON=@Effect
LOCAL.CASTER=<UID>
LOCAL.DAMAGE=<EVAL <F_COMPUTE_POWERLEVEL 10>+<R0,2>>
SOUND=0244
EFFECT 3,03709,3,30,0,9965
EFFECT 3,0376A,3,30,0,9502
EFFECT 3,0375A,3,30
FORCHARS 6
    IF (<ISBENEFICIAL <LOCAL.CASTER>>) && (<SRC.UID> != <UID>)
        IF (<CANSEELOS <LOCAL.CASTER>>)
        CALL F_NOBLESACRIFICE_STUFF
            IF (<LOCAL.SACRIFICE>)
            SRC.SOUND=0423
            SRC.HITS=1
            SRC.STAM=1
            SRC.MANA=1
            ENDIF
        ENDIF
    ENDIF
ENDFOR

[Spell 209]
DEFNAME=s_Remove_Curse
NAME=Remove Curse
SOUND=
RUNES=.@,,2 1060726
CAST_TIME=1.5
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_targ_char|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=10
DURATION=0.0
MANAUSE=20
SKILLREQ=SKILL_CHIVALRY 5.0
INTERRUPT=0.0,0.0

ON=@Effect
IF (<SRC.KARMA> < -5000)
LOCAL.CHANCE=0
ELIF (<SRC.KARMA> < 0)
LOCAL.CHANCE=<EVAL ((SQRT(20000+<SRC.KARMA>))-122)>
ELIF (<SRC.KARMA> < 5625)
LOCAL.CHANCE=<EVAL ((SQRT(<SRC.KARMA>))+25)>
ELSE
LOCAL.CHANCE=100
ENDIF
IF (<LOCAL.CHANCE> >= <R1,100>)
SOUND=0F6
SOUND=01F7
EFFECT 3,03709,1,30,,13
    IF (<FINDID.i_rune_weaken>)
    FINDID.i_rune_weaken.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_paralyze>)
    FINDID.i_rune_paralyze.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_clumsy>)
    FINDID.i_rune_clumsy.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_feeblemind>)
    FINDID.i_rune_feeblemind.TIMERD=1
    ENDIF
    IF (<FINDID.i_rune_curse>)
    FINDID.i_rune_curse.TIMERD=1
    ENDIF
ELSE
SOUND=01DF
ENDIF

[Spell 210]
DEFNAME=s_Sacred_Journey
NAME=Sacred Journey
SOUND=
RUNES=.@,,2 1060727
CAST_TIME=1.5
RESOURCES=
RUNE_ITEM=0
SCROLL_ITEM=0
FLAGS=spellflag_playeronly|spellflag_targ_item|spellflag_nofreezeoncast
EFFECT_ID=
EFFECT=15
DURATION=0.0
MANAUSE=10
SKILLREQ=SKILL_CHIVALRY 15.0
INTERRUPT=0.0,0.0

ON=@Start
IF (<FLAGS>&STATF_CRIMINAL)
SYSMESSAGE @,,2 1005561
RETURN 1
ELIF (<FLAGS>&STATF_WAR)
SYSMESSAGE @,,2 1061282
RETURN 1
ELIF (<WEIGHT> > <MAXWEIGHT>)
SYSMESSAGE @,,2 502359
RETURN 1
ENDIF

ON=@Effect
IF (<ISITEM>)
    IF (<TYPE> != T_RUNE)
    SRC.MESSAGE @,,2 502357 //you can not recall from that object
    ELIF (!(<MOREX>))
    SRC.MESSAGE @,,2 501805 //that rune is not yet marked
    ELIF (<SERV.MAP(<MOREX>,<MOREY>,<MOREM>).REGION.FLAGS>&region_antimagic_recall_in)
    SRC.SYSMESSAGE @,,2 1019004  // You are not allowed to travel there.
    ELIF (<SRC.REGION.FLAGS>&region_antimagic_recall_out)
    SRC.SYSMESSAGE @,,2 501035    //You cannot teleport from here to the destination.
    ELIF (<SRC.FLAGS>&STATF_CRIMINAL)
    SRC.SYSMESSAGE @,,2 1005561 // Thou'rt a criminal and cannot escape so easily.
    ELIF (<SRC.FLAGS>&STATF_WAR)
    SRC.SYSMESSAGE @,,2 1061282 // You cannot use the Sacred Journey ability to flee from combat.
    ELIF (<SRC.WEIGHT> > <SRC.MAXWEIGHT>)
    SRC.SYSMESSAGE @,,2 502359 // Thou art too encumbered to move.
    ELSE
    SRC.GO <MOREP>
    ENDIF
ENDIF

[EOF]