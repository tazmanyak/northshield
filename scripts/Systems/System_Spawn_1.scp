[FUNCTION spawn]
IF !(STRCMP(<ARGS>,?))
	SRC.SYSMESSAGE Opens up the spawn creation dialog.
	SRC.SYSMESSAGE Format: spawn ARGUMENT
	SRC.SYSMESSAGE Where ARGUMENT can be 1 or 2.  1 will recreate the last spawn point set up.
	SRC.SYSMESSAGE 2 will clear spawn settings.
	RETURN 1
ENDIF
IF (<ARGN> == 1)
	Serv.NEWITEM=i_worldgem_bit
	new.P=<P>
	new.TYPE=34
	new.AMOUNT=<TAG.SPAWNAMOUNT>
	new.MORE=<TAG.SPAWNID>
	new.MOREX=<TAG.MINTIME>
	new.MOREY=<TAG.MAXTIME>
	new.MOREZ=<TAG.MAXDIST>
	new.ATTR=0b0
	new.DCLICK
	SRC.MESSAGE=@1953,,1 
	SRC.sysMESSAGE=@1953,,1 Spawn yaratildi, <ACT.TIMER> saniye sonra aktif olacak.
	RETURN 1
ENDIF
IF (<ARGN> == 2)
	SRC.TAG.SPAWNAMOUNT=0
	SRC.TAG.SPAWNID=0
	SRC.TAG.MINTIME=0
	SRC.TAG.MAXTIME=0
	SRC.TAG.MAXDIST=0
	SRC.MESSAGE Spawn settings cleared.
	RETURN 1
ENDIF
IF !(0<SRC.TAG.SPAWNAMOUNT>)
	SRC.TAG.SPAWNAMOUNT=1
	SRC.TAG.SPAWNID=c_man	
	SRC.TAG.MINTIME=5
	SRC.TAG.MAXTIME=10
	SRC.TAG.MAXDIST=5
ENDIF
SRC.DIALOG d_spawnkur
RETURN 1

[DIALOG d_spawnkur]
10,10
resizepic 400 370 3500 350 215
resizepic 410 380 5054 331 196
page 0
text 500 375 1000 0
text 405 395 1000 1
button 550 395 2440 2440 1 0 1
text 560 396 1000 6
text 405 425 1000 2
button 550 425 2440 2440 1 0 2
text 560 426 1000 7
text 405 455 1000 3
button 550 455 2440 2440 1 0 3
text 560 456 1000 8
text 405 485 1000 4
button 550 485 2440 2440 1 0 4
text 560 486 1000 9
text 405 515 1000 5
button 550 515 2440 2440 1 0 5
text 560 516 1000 10
button 415 545 2076 2075 1 0 6
button 615 545 2073 2072 1 0 0

[DIALOG d_spawnkur TEXT]
  Spawn Olusturma
  Sayisi
  Npc ID
  Min.suresi
  Max.suresi
  Max.uzakligi
<eval <SRC.TAG.SPAWNAMOUNT>>
<SRC.TAG.SPAWNID>
<eval <SRC.TAG.MINTIME>>
<eval <SRC.TAG.MAXTIME>>
<eval <SRC.TAG.MAXDIST>>

[DIALOG d_spawnkur BUTTON]
ONBUTTON=0
SYSMESSAGE=Spawn Iptal Edildi.
RETURN 0

ONBUTTON=1
INPDLG TAG.SPAWNAMOUNT 3
RETURN 1

ONBUTTON=2
INPDLG TAG.SPAWNID 20
RETURN 1

ONBUTTON=3
INPDLG TAG.MINTIME 10
RETURN 1

ONBUTTON=4
INPDLG TAG.MAXTIME 10
RETURN 1

ONBUTTON=5
INPDLG TAG.MAXDIST 5
RETURN 1

ONBUTTON=6
NEWITEM=01ea7
ACT.P=<P>
ACT.TYPE=34
ACT.AMOUNT=<TAG.SPAWNAMOUNT>
ACT.MORE=<TAG.SPAWNID>
ACT.MOREX=<TAG.MINTIME>
ACT.MOREY=<TAG.MAXTIME>
ACT.MOREZ=<TAG.MAXDIST>
ACT.ATTR=0b0
ACT.DCLICK
sysMESSAGE=@1953,,1 Spawn yaratildi, <ACT.TIMER> saniye sonra aktif olacak.
//MESSAGE=First creature will spawn in <ACT.TIMER> seconds.
RETURN 0

[EOF]