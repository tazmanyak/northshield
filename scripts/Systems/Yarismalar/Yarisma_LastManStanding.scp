
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////Last Man Standing - Avatar////////////////////////////////////////////////////////////


[defname LMS_options]
LMS_player_base			"5951,319"

LMS_katilimci_baraji		"2" // Eklenen sayidan kucukse iptal edilir

LMS_finish_and_go		"5322,1822"

LMS_open_gate_p			"5321,1813"
LMS_timer_gate			"600"

/////////////////////////////////////////////////////////////////////////Function Start

[function f_LMS_open]
serv.allclients f_LMS_draw
var.LMS_katilimcisi	0
var.LMS_katilimcisi_x	0

Serv.newitem i_gate_LMS
new.p <def.LMS_open_gate_p>
new.timer <def.LMS_timer_gate>
var.yarisma_gate <new.uid>

[function f_LMS_start]


if (<isevent.e_yarisma_LMS>)
invis 0
Yarisma_Duyurusu Yarisma basladi !
endif

[function f_LMS_winner]
if <isevent.e_yarisma_LMS>
var.son_last <name>	 // winner
go <def.LMS_finish_and_go>
say @,,1 Kazandiniz !
f_LMS_fame_karma_kill_load
invis 0
f_option_mysql 0
db.connect
db.execute "insert into son_etkinlikler (tarih, kazanan, yarisma) VALUES ('<addslashes <serv.rtime>>','<addslashes <name>>','Last Man Standing');"
db.close
serv.newitem i_y_kupa
new.cont <findlayer(layer_bankbox)>
serv.newitem might_silah
new.cont <findlayer(layer_bankbox)>
new.color 07a1
new.name Etkinlik Odulu
// account.tag0.connecttimeused -= 20
sysmessage @,,1 Odulleriniz bankaniza aktarilmistir.
// sysmessage @,,1 Might Random Magical silah ve 20 saat online sure kazandiniz.
gold += <eval (<var.LMS_katilimcisi_X>)*15000> // Katilimci sayisi X 20k gold --> Odul
serv.duyur Last Man Standing, kazanan <name> olmustur.
var.etkinlikprogramisondurum Last Man Standing yarismasi bitti , kazanan <name> olmustur.
events -e_yarisma_LMS
findid.i_bleeder.remove
findid.i_rune_poison.remove
endif

[function f_LMS_loser]


if (<eval <var.LMS_katilimcisi>> == 1)
serv.allclients f_LMS_winner
go <def.LMS_finish_and_go>
f_LMS_fame_karma_kill_load
//serv.newitem i_gold
//new.amount 2500
//new.bounce
invis 0
else

go <def.LMS_finish_and_go>
f_LMS_fame_karma_kill_load
//serv.newitem i_gold
///new.amount 2500
//new.bounce
say @33,,1 Kaybettiniz.
findid.i_bleeder.remove
findid.i_rune_poison.remove
invis 0
endif


[function f_LMS_loser_logout]
events -e_yarisma_LMS
var.LMS_katilimcisi --
f_LMS_loser
invis 0

[function f_LMS_draw]
if <isevent.e_yarisma_LMS>
go <def.LMS_finish_and_go>
f_LMS_fame_karma_kill_load
invis 0
events -e_yarisma_LMS

//serv.newitem i_gold
//new.amount 2500
//new.bounce
Yarisma_Duyurusu Last Man Standing yarismasi iptal edildi.
endif

//////////////////////////////////////////////////

[function f_LMS_fame_karma_kill_save]
tag0.ctf_kill	<kills>
tag0.ctf_fame	<fame>
tag0.ctf_karma	<karma>

[function f_LMS_fame_karma_kill_load]
kills	<eval <tag0.ctf_kill>>
fame	<eval <tag0.ctf_fame>>
karma	<eval <tag0.ctf_karma>>
flags &= ~statf_criminal
tag0.override.noto
tag0.ctf_kill
tag0.ctf_fame
tag0.ctf_karma
binekstabil



/////////////////////////////////////////////////////////////////////////function finish
/////////////////////////////////////////////////////////////////////////Events Start

[events e_yarisma_LMS]

on=@death
var.LMS_katilimcisi --
events -e_yarisma_LMS
f_LMS_loser
hits <str>
return 1

on=@login
ref1 <var.yarisma_gate>
if !(<ref1.timer> > 1) 
	src.f_LMS_loser_logout
endif

on=@logout
ref1 <var.yarisma_gate>
if !(<ref1.timer> > 1) 
	src.f_LMS_loser_logout
endif

on=@spellcast
if (<serv.spell.<argn>.flags>&SPELLFLAG_SUMMON) || (<serv.spell.<argn>.flags>&spellflag_field)
src.f_fizzle
action -1
return 1
endif

if (<argn> == 40) || (<argn> == 22) || (<argn> == 45) || (<argn> == 44) || (<argn> == 57) || (<argn> == 55) || (<argn> == 54) || (<argn> == 52) || (<argn> == 49)
src.f_fizzle
action -1
return 1
endif


on=@itemdclick
if (<act.baseid> == i_runebook) || (<act.baseid> == i_shrink_deed) || (<act.type> == t_figurine)
src.sysmessage @,,1 Yarismada bu islem yapilamaz.
return 1
endif

on=@mount
return 1

on=@skillstart
if (<action> == (skill_hiding)) || (<action> == id(skill_hiding)) || (<action> == id(skill_stealth)) || (<action> == (skill_stealth))
src.sys_error Hide olamazsiniz..
skill fail
return 1
endif

/////////////////////////////////////////////////////////////////////////events finish
/////////////////////////////////////////////////////////////////////////Item Start

[itemdef i_gate_LMS]
id i_moongate_blue
name Last Man Standing Etkinligi

on=@click
message @0808,1,1 Last Man Standing Etkinligi
return 1

on=@create
attr 08000
color 09a9

on=@dclick
if (<src.findlayer.25>) 
src.sysmessage @1953,1,1,1 Binek üzerinde katılamazsınız.
return 1
endif

local.uid1 <src.uid>
forchars 10
if (<memoryfindtype.memory_ipet>)
if (<memoryfindtype.memory_ipet.link>==<local.uid1>)
try uid.<local.uid1>.sysmessage @1953,1,1,1 Etrafınızda bineğiniz olduğu taktirde yarışmaya katılamazsınız.
return 1
endif
endif
endfor
local.uid1
if <src.findtype.t_eq_trade_window>
	src.sys_error Trade ekraninda yarışmaya katılamazsınız.
	return 1
endif
if <src.account.plevel> > 1
return 1
endif
src.hits <src.str>
SRC.FINDID.i_rune_protection.REMOVE
SRC.FINDID.i_rune_reactive_armor.REMOVE

src.findid.i_rune_bless.remove
src.findid.i_rune_agility.remove
src.findid.i_rune_strength.remove
var.LMS_katilimcisi ++
src.invis 1
//src.binekolmazsaolmaz
src.sysmessage @69,,1 Suan gorunmezsiniz lutfen alana yerlesin yarisma birazdan baslayacaktir.
serv.allclients Yarisma_katilan <src.name> yarışmaya katıldı. Toplam katılımcı : <eval <var.LMS_katilimcisi>>

src.events +e_yarisma_LMS
src.go <def.LMS_player_base>
src.f_LMS_fame_karma_kill_save

on=@step
src.sysmessage @39,1,1 <src.name>, cift tiklayin.
return 1


on=@timer

if (<eval <var.LMS_katilimcisi>> < <def.LMS_katilimci_baraji>)
serv.allclients f_LMS_draw
serv.allclients Yarisma_Duyurusu Katilimci sayisi baraji gecemedigi icin Last Man Standing yarismasi iptal edildi.
var.etkinlikprogramisondurum Yarisma_Duyurusu Katilimci sayisi baraji gecemedigi icin Last Man Standing yarismasi iptal edildi.
var.katilim 0
remove
return 1
endif

var.LMS_katilimcisi_X <var.LMS_katilimcisi>
serv.allclients Yarisma_Duyurusu Last Man Standing yarismasi basliyor.Katilimci : <eval <var.LMS_katilimcisi>> //, odul : <eval (<var.LMS_katilimcisi_X>*5000)> Gold.
serv.allclients f_LMS_start
var.katilim 0
remove
return 1



///////////////////////////////////////////////////////////////////////////////Bilgi Deed & Dialog
[DIALOG d_LMS_bilgi_dialog]
0,0
page 0
src.closealldialogs
resizepic 250 10 9200 385 195	
gumppictiled 260 20 365 175 2624

htmlgump 270 30 345 154 0 1 1

[DIALOG d_LMS_bilgi_dialog text]
asd

[itemdef i_LMS_bilgi_deed]
id i_deed
name LMS BILGI
type t_normal

on=@create
attr 08000
color 0810

on=@click
src.dialog d_LMS_bilgi_dialog

[EOF]

